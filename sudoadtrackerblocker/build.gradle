apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: "com.cookpad.android.plugin.license-tools"

android {
    compileSdkVersion 34
    namespace "com.sudoplatform.sudoadtrackerblocker"
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testApplicationId = "com.sudoplatform.sudoadtrackerblocker.test"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets {
        // add a shared test folder to share code between Robolectric and Android tests
        def sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    testOptions {
        animationsDisabled = true
        unitTests.all {
            outputs.upToDateWhen {false}
        }
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        pickFirst("META-INF/sudoadtrackerblocker_debug.kotlin_module")
    }

    ndkVersion '25.2.9519653'

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation "androidx.core:core-ktx:1.13.1"

    // For adblock rust library built with uniffi. 
    compileOnly "net.java.dev.jna:jna:5.14.0@aar"

    // AWS
    //
    // AWS SDKs do not follow semantic versioning rules so we need to strictly specify AWS SDK versions.
    // Follow the process at https://anonyome.atlassian.net/wiki/spaces/PLA/pages/840073287/AWS+Android+SDK+versions+to+use+for+platform+SDKs
    // for updating AWS SDK versions.
    implementation "com.amazonaws:aws-android-sdk-core:$aws_version!!"
    implementation "com.amazonaws:aws-android-sdk-s3:$aws_version!!"
    constraints {
        implementation("com.squareup.okhttp3:okhttp:4.9.3") {
            because("Versions < 4.9.3 has CVE-2021-0341")
        }
    }

    // Sudo Platform SDKs
    implementation "com.sudoplatform:sudologging:5.0.0"
    implementation "com.sudoplatform:sudoconfigmanager:8.0.0"
    implementation "com.sudoplatform:sudouser:20.0.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.12.1'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
    testImplementation "io.kotlintest:kotlintest-assertions:3.4.2"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.mockito:mockito-core:5.11.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.2.1"
    testImplementation "androidx.test:core:1.6.1"
    testImplementation 'org.awaitility:awaitility:3.1.6'
    testImplementation "com.jakewharton.timber:timber:5.0.1"
    testImplementation "org.json:json:20220320"

    testRuntimeOnly 'org.slf4j:slf4j-jdk14:2.0.0' // To suppress log noise

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.test:core:1.6.1"
    androidTestImplementation "io.kotlintest:kotlintest-assertions:3.4.2"
    androidTestImplementation 'org.awaitility:awaitility:3.1.6'
    androidTestImplementation "com.google.guava:guava:31.1-android"
    androidTestImplementation "com.sudoplatform:sudokeymanager:8.0.0"
    androidTestImplementation "com.jakewharton.timber:timber:5.0.1"

    androidTestImplementation "net.java.dev.jna:jna:5.14.0@aar"
}

// Returns a list of `configurations` which should not be scanned for vulnerabilities
def getConfigurationsToSkip = { configs ->
    configs.findAll {
        it.name.startsWithAny("androidTest", "test", "lint") ||
                it.name.contains("AndroidTest") ||
                it.name.contains("UnitTest")
    }.collect {
        it.name
    }
}

tasks.register("printConfigurationsToSkip") {
    group = "OWASP dependency-check"
    description = "Print the configurations which are skipped when scanning for vulnerable dependencies"
    doLast {
        println "Configurations to skip when scanning for vulnerable dependencies:"
        println getConfigurationsToSkip(configurations).join("\n")
    }
}

afterEvaluate {
    // NOTE: this must be within `afterEvaluate` to ensure all the configurations have been created before filtering them
    dependencyCheck {
        suppressionFile file("../dependency-suppression.xml").toString()
        failBuildOnCVSS 0
        skipConfigurations = getConfigurationsToSkip(configurations)
        cve {
            urlModified = "https://anonyome-nist-cve-mirror.s3.amazonaws.com/nvdcve-1.1-modified.json.gz"
            urlBase = "https://anonyome-nist-cve-mirror.s3.amazonaws.com/nvdcve-1.1-%d.json.gz"
            startYear = 2002
            waitTime = 0
        }
        analyzers {
            assemblyEnabled = false
            ossIndex {
                username = "${ossIndexUsername}"
                password = "${ossIndexPassword}"
                warnOnlyOnRemoteErrors = true
            }
        }
    }
}


// Kotlin Lint
apply plugin: 'org.jmailen.kotlinter'

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
}

// Exclude the autogenerated adblock filter engine from lint and formatting.
tasks.named("lintKotlinMain").configure {
    exclude("**/*/filter_engine.kt")
}
tasks.named("formatKotlinMain").configure {
    exclude("**/*/filter_engine.kt")
}

// Generate .editorconfig in the root directory of the project being built
static void generateEditorConfig(final Project project) {
    File editorConfig = new File("${project.rootDir}/.editorconfig")
    editorConfig.write("root = true\n")
    editorConfig.write("[*.{kt,kts}]\n")
    editorConfig.append("indent_style=space\n")
    editorConfig.append("indent_size=4\n")
    editorConfig.append("max_line_length=140\n")
    editorConfig.append("ij_continuation_indent_size=4\n")
    editorConfig.append("trim_trailing_whitespace=true\n")
    editorConfig.append("insert_final_newline=true\n")
    editorConfig.append("[*.xml]\n")
    editorConfig.append("trim_trailing_whitespace=true\n")
    editorConfig.append("insert_final_newline=true\n")
}

afterEvaluate {
    generateEditorConfig(project)
}

// Jacoco test coverage
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.10"
}

task jacocoTestReport(type: JacocoReport, dependsOn:"testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    classDirectories.setFrom(
        fileTree(
            dir: "${buildDir}/tmp/kotlin-classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
        )
    )
    sourceDirectories.setFrom(files("${buildDir.parent}/sudoadtrackerblocker/src/main/java"))
    executionData.setFrom(fileTree(["dir":"$buildDir", "includes":["**/*.exec", "**/*.ec"]]))
}

// Dokka documentation generation
apply plugin: 'org.jetbrains.dokka'

dokkaHtml {
    outputDirectory.set(project.file("$buildDir/javadoc"))

    dokkaSourceSets {
        configureEach {
            jdkVersion.set(8)
            skipEmptyPackages.set(true)
            includes.from("packages.md")
        }
    }
}

// Setup common publishing variables
ext {
    projectGroup = 'com.sudoplatform'
    projectArtifact = project.name
    projectDescription = 'Advertising and Tracker Blocker SDK for the Sudo Platform by Anonyome Labs.'
    projectUrl = 'https://github.com/sudoplatform/sudo-ad-tracker-blocker-android'
    projectSCM = 'scm:git:github.com/sudoplatform/sudo-ad-tracker-blocker-android.git'
    if (project.findProperty("tag")) {
        projectVersion = "$tag"
    } else {
        projectVersion = project.android.defaultConfig.versionName
    }
}

// Internal and External publishing
if (project.findProperty("tag") && project.file("${rootProject.projectDir}/util/publish-mavencentral.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/publish-mavencentral.gradle"
} else if (project.file("${rootProject.projectDir}/util/publish-internal-android.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/publish-internal-android.gradle"
}

// Sonarqube code analysis
if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
    ext.sonarProjectKey = "sudo-adtrackerblocker-android"
    ext.sonarProjectName = "sudo-adtrackerblocker-android"
    ext.sonarExclusions = ["*.png", "*.jks", "*.json", "*.key"]
    apply from: "${rootProject.projectDir}/util/sonarqube.gradle"
}

// License checking
licenseTools {
    licensesYaml = file("licenses.yml")
    ignoredGroups = ["com.anonyome", "com.sudoplatform"]
    ignoredProjects = ["org.mockito"]
}
@Grab('org.yaml:snakeyaml:1.17') // Yaml parser
import org.yaml.snakeyaml.Yaml
task checkLicenseHaveRequiredFields {
    description = "Check every license entry has the mandatory fields"
    group = "verification"
    doLast {
        def mandatoryFields = ["artifact", "name", "copyrightHolder", "url"]
        Yaml parser = new Yaml()
        def licenses = parser.load((file("licenses.yml") as File).text)
        def errors = new StringBuilder()
        licenses.each { artifact ->
            mandatoryFields.each { field ->
                if (artifact[field] == null) {
                    errors.append("License entry '${artifact.artifact}' is missing required element `$field`\n")
                }
            }
            if (artifact["license"] == null && artifact["licenseUrl" == null]) {
                errors.append("License entry '${artifact.artifact}' must have one of [license,licenseUrl]\n")
            }
        }
        if (errors.length() > 0) {
            throw new GradleException("Missing license elements:\n$errors")
        }
    }
}
checkLicenses.finalizedBy checkLicenseHaveRequiredFields

